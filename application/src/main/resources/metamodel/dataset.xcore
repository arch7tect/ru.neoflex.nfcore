@GenModel(
    bundleManifest="false",
    modelDirectory="ru.neoflex.nfcore.datasource/src/java/java-gen",
    complianceLevel="7.0",
    rootExtendsClass="org.eclipse.emf.ecore.impl.MinimalEObjectImpl",
    rootExtendsInterface="org.eclipse.emf.ecore.EObject"
)
package ru.neoflex.nfcore.dataset
import ru.neoflex.nfcore.base.types.QName
import ru.neoflex.nfcore.base.types.JSON
import ru.neoflex.nfcore.base.types.Text
import ru.neoflex.nfcore.base.types.Password
import ru.neoflex.nfcore.base.auth.Audit
import ru.neoflex.nfcore.application.Typography

/*JdbcDriver*/
@Ecore(constraints="IsValid")
class JdbcDriver {
    QName name
    String driverClassName
	String[] extLibraries /*доп. jar файлы*/
	String urlPattern
}

/*JdbcConnection*/
@Ecore(constraints="IsValid")
class JdbcConnection {
    QName name
    refers JdbcDriver driver
    String url
    String userName
    Password password
}

/*Dataset*/
abstract class Dataset {
    QName name
    op JSON deleteAllColumns()
    op JSON loadAllColumns()
    op JSON runQueryDataset()
    contains DatasetColumn[] datasetColumn
}

@Ecore(constraints="IsValid")
class JdbcDataset extends Dataset {
    refers JdbcConnection connection
    QueryType queryType
    Text query //тут, так как для каждого dataset своя валидаия
    Text schemaName
    Text tableName
    op JSON showAllTables()
}

/*View*/
abstract class DatasetSettings {
     QName name //default сделать по умолчанию (не редактируемый)
     contains Audit audit // createdBy + created(time) + modifiedBy + modified(time)
     Access access //Публичный, закрытый доступ
     op JSON createAllColumns()
     op JSON deleteAllColumns()
     boolean firstCheckBoxColumn //есть ли в первом столбце в гриде столбец с чекбоксами или его нет
     refers Dataset dataset
     contains DatasetColumnView[] column
     op JSON runQueryDatasetSettings()
}

class DatasetSettingsGrid extends DatasetSettings {
     String rowCount//сколько строк показывать (выбрать из списка)
     contains Highlight[] highlight//При условии таком-то, строки заливаются таким-то цветом
     Theme theme //Выбранная тема грида
     boolean showUniqRow//Показывать только уникальные строки
     contains Conditions[] serverFilter //Состав серверного фильтра
     String clientFilterExpression //выражение в клиентском фильтре
}

class DatasetSettingsPivot extends DatasetSettings {
}

class DatasetSettingsDiagram extends DatasetSettings {
}

class Highlight {
    QName name
    HighlightType highlightType
    Colors backgroundColor
    Colors textColor
    refers DatasetColumnView datasetColumnView
    Operations operation
    String value
}

class DatasetColumn {  //не редактирумый класс, информативный. Отвечает на вопрос: что пришло с сервера?
    String name //rdbmsFieldName название поля с сервера
    String rdbmsDataType //тип, который приходит с сервера (не измененный)
    DataType convertDataType //rdbmsDataType преобразованный в какой-то из enum DataType
}

abstract class DatasetColumnView {
    QName name
    contains Typography headerName //название столбца
    refers FormatMask formatMask//Формат того, как отображаются данные (два знака после запятой, с точнок, как текст, как число...)
    String headerTooltip //всплывающая подсказка
    boolean hide //скрытый ли столбец. По умолчанию false
    Pinned pinned //зафиксированный ли столбец (слева, справа)
    Filter filter //выбор типа фильтра клиентского на столбце. Зависит от типа данных, но можно изменить.
    Sort sort //тип клиентской сортировки
    String defaultValue //Если пришло значение null, то что отображать в ячейке
    Aggregate aggregate //Посчитать столбец (сумму, количество, вычислить максимальное значение)
}

class RdbmsColumn extends DatasetColumnView {
    refers DatasetColumn datasetColumn
}

class CustomColumn extends DatasetColumnView {
    String customColumnExpression //выражение в вычисляемом столбце
}

class FormatMask {
    QName name
    String value // ( p.5 234.10 || 5 234.10 || 5 234.1000 || 5 234)
}

class Conditions {
    String name
    refers DatasetColumn datasetColumn
    Operations operation
    String value
    boolean enable //включен ли фильтр
}

enum QueryType {
    UseTableName
    UseQuery
}

enum HighlightType {
    Row
    Column
}

enum Colors {
    Yellow
    Green
    Blue
    Orange
    Red
}

enum Operations {
    EqualTo
    GreaterThan
    GreaterThenOrEqualTo
    IncludeIn
    LessThan
    LessThenOrEqualTo
    NotEqual
}

enum Theme {
    Balham
    Blue
    Bootstrap
    Fresh
    Material
}

enum Aggregate {
    Average
    Count
    CountDistinct
    Maximum
    Median
    Minimum
    Sum
}

enum Sort {
    Undefined
    FromAtoZ
    FromZtoA
}

enum Access {
    Public //A Number Filter for number comparisons.
    Private //A Text Filter for string comparisons.
}

enum Filter {
    TextColumnFilter //A Text Filter for string comparisons.
    NumberColumnFilter //A Number Filter for number comparisons.
    DateColumnFilter //A Date Filter for date comparisons.
}

enum Pinned {
    Undefined
    Left
    Right
}

enum DataType {
    Undefined
    Boolean
    Date
    Decimal
    Integer
    String
    Timestamp
}
